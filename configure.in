dnl Process this file with autoconf to produce a configure script.
dnl
dnl Copyright (c) 2002-2003 Rob Peters rjpeters@klab.caltech.edu
dnl
dnl $Revision$

AC_INIT(GroovX, [1.0a1], [rjpeters@klab.caltech.edu])
AC_PREREQ(2.53)
AC_REVISION($Revision$)

### New macro definitions

# AC_CHECK_LIB_CXX(libname, includes, function-call,
#                  action-on-success, action-on-fail)
AC_DEFUN([AC_CHECK_LIB_CXX],
	 [AC_MSG_CHECKING([for lib$1])
	 libs_save=$LIBS
	 LIBS="$LIBS -l$1"
	 AC_TRY_LINK([$2], [$3], [havelib_$1=yes], [havelib_$1=no])
	 LIBS=$libs_save
	 if test $havelib_$1 = yes; then
	    AC_MSG_RESULT(yes)
	    $4
	 else
	    AC_MSG_RESULT(no)
	    $5
	 fi])

# If CXXFLAGS was not already set, then set it now to keep autoconf from
# adding "-g -O2".

if test "${CXXFLAGS+set}" != "set" ; then
    CXXFLAGS=""
fi

### Find operating system.

AC_MSG_CHECKING([for operating system])

OS=`uname`

AC_MSG_RESULT($OS)

### Find c++ compiler.

AC_LANG(C++)
AC_PROG_CXX(g++-3 g++ c++ CC)

case ${CXX}-${OS} in
   g++*)
      CXXFLAGS="$CXXFLAGS -W -Wdeprecated -Wall"
      CXXFLAGS="$CXXFLAGS -Wsign-promo -Wwrite-strings"
      GCC_VERSION=`$CXX -dumpversion`
      case $GCC_VERSION in
         2.9*) ;;
	 3.*) CXXFLAGS="$CXXFLAGS -Wno-system-headers" ;;
      esac
      case `uname -m` in
         i686) CXXFLAGS="$CXXFLAGS -march=i686";;
      esac
      case $OS in
         Darwin)
	    CXXFLAGS="$CXXFLAGS -dynamic"
	    # Need to use -install_name ${LIB_RUNTIME_DIR}/libname?
	    SHLIB_CMD="$CXX -dynamiclib -flat_namespace -undefined suppress -o"
	    STATLIB_CMD="libtool -static -o"
	    ;;
	 *)
	    SHLIB_CMD="$CXX -shared -o"
	    STATLIB_CMD="ar rus"
	    ;;
      esac
      cxxflags_debug="-O1 -g"
      cxxflags_prod="-O2"
      ;;
   CC-IRIX)
      CPPFLAGS="$CPPFLAGS -I$(HOME)/local/$(ARCH)/include/cppheaders"
      CXXFLAGS="$CXXFLAGS -mips3 -n32 -ptused -no_prelink -no_auto_include"
      CXXFLAGS="$CXXFLAGS -LANG:std -LANG:exceptions=ON"
      cxxflags_debug="-g -O0"
      # -O3 gave little improvement over -O2
      cxxflags_prod="-O2"
      SHLIB_CMD="$CXX -shared -Wl,-check_registry,/usr/lib32/so_locations -o"
      STATLIB_CMD="$CXX -ar -o"
      ;;
   *)
      AC_MSG_WARN([unknown CXX-OS pair ${CXX}-${OS}])
      cxxflags_debug="-g -O1"
      cxxflags_prod="-O2"
      SHLIB_CMD="$CXX -shared -o"
      STATLIB_CMD="ar rus"
      ;;
esac

AC_SUBST(SHLIB_CMD)
AC_SUBST(STATLIB_CMD)

### Arch-specific stuff

need_rpath=yes

case $OS in
   Linux)
      need_rpath=yes
      SHLIB_EXT=so
      STATLIB_EXT=a
      # Need this to allow symbols from the executable to be accessed by loaded
      # dynamic libraries; this is needed e.g. for the matlab libut.so library to
      # find the "_start" symbol.
      LDFLAGS="$LDFLAGS -Wl,--export-dynamic"
      ;;
   IRIX)
      need_rpath=yes
      SHLIB_EXT=so
      STATLIB_EXT=a
      AC_DEFINE(SHORTEN_SYMBOL_NAMES,1,[shorten symbol names?])
      ;;
   Darwin)
      need_rpath=no
      SHLIB_EXT=dylib
      STATLIB_EXT=a
      AC_DEFINE(ESD_WORKAROUND,1,[Darwin esd workaround])
      # The /sw/lib and /sw/include directories are managed by Fink
      CPPFLAGS="$CPPFLAGS -I/sw/include"

      # We are slightly bending the rules here by putting a "-l" directive
      # in LDFLAGS (which normally should only have "-L" directives
      LDFLAGS="$LDFLAGS -L/sw/lib -lcc_dynamic"
      ;;
   *)
      AC_MSG_ERROR([unknown operating system $OS])
      ;;
esac

AC_SUBST(SHLIB_EXT)
AC_SUBST(STATLIB_EXT)

### Check whether virtual base classes are OK

AC_MSG_CHECKING([whether virtual base classes are properly implemented])

AC_TRY_RUN([void* p1 = 0; void* p2 = 0;

struct A {
  virtual ~A() {};
  virtual void foo() { delete this; }
  int i;
};

struct B : public virtual A {
  virtual ~B() { p2 = (void*)this; }
};

int main()
{
  B* b = new B;
  p1 = (void*)b;
  b->foo();

  return (p1 == p2) ? 0 : 1;
}],
[AC_MSG_RESULT(yes)],
[AC_MSG_RESULT(no);
 AC_MSG_ERROR([$CXX is broken: has a buggy implementation of virtual base classes])])

### Check whether "return void" is allowed in templates

AC_MSG_CHECKING([whether return void is allowed in templates])

AC_TRY_COMPILE([
template <class R>
struct Foo
{
  template <class Func>
  static R bar(Func f)
  {
    return f();
  }
};

void baz() {}],
[Foo<void>::bar(baz);],
[AC_MSG_RESULT(yes)],
[AC_MSG_RESULT(no);
 AC_MSG_ERROR([$CXX is broken: does not allow return void in templates])])

### Check various template peculiarities

AC_MSG_CHECKING([whether we can take the address of template functions])

AC_TRY_COMPILE([namespace Q {
  template <class T>
  void foo(T) {}
}
template <class F> void bar(F) {}
],
[bar(Q::foo<int>); bar(Q::foo<double>);],
[AC_MSG_RESULT(yes)],
[AC_DEFINE(BROKEN_TEMPLATE_FUNCTIONS,1,[can take address of template funtions?])
 AC_MSG_RESULT(no)])

### Check c++ name mangling

AC_MSG_CHECKING([whether typeinfo::name() gives a mangled typename])

AC_TRY_RUN([#include <typeinfo>
#include <string.h>

namespace Q {
template <class T>
  struct foo {};
}

struct bar {};

int main()
{
  extern Q::foo<bar> p;
  return (strcmp("Q::foo<bar>", typeid(p).name()) == 0) ? 0 : 1;
}
],
[AC_DEFINE(NO_TYPENAME_MANGLING,1,[is typeid().name() unmangled?]) AC_MSG_RESULT(no)],
AC_MSG_RESULT(yes))

### Check support for c++ iostreams

AC_MSG_CHECKING([whether c++ compiler supports standard iostreams])

AC_TRY_COMPILE([#include <iostream>], [std::ios_base* p;],
		[AC_MSG_RESULT(yes)],
		[AC_DEFINE(PRESTANDARD_IOSTREAMS,1,[prestandard iostreams?])
                 AC_MSG_RESULT(no)])

have_std_io=no

AC_MSG_CHECKING([whether iostreams are in std namespace])

AC_TRY_COMPILE([#include <iostream>], [std::iostream* p;],
		have_std_io=yes)

if test $have_std_io = no; then
   AC_TRY_COMPILE([#include <iostream.h>], [std::iostream* p;],
		   have_std_io=yes)
fi

AC_MSG_RESULT($have_std_io)

if test $have_std_io = yes; then
   AC_DEFINE(STD_IO,std,[iostreams are in std namespace])
else
   AC_DEFINE(STD_IO,[],[iostreams are in global namespace])
fi

### Checks for header files.
AC_PATH_XTRA

CPPFLAGS="$CPPFLAGS $X_CFLAGS"
LDFLAGS="$LDFLAGS $X_LIBS"

AC_CHECK_HEADERS([limits iosfwd iostream fstream sstream \
		  png.h jpeglib.h fcntl.h termios.h sys/time.h unistd.h \
		  ext/stdio_filebuf.h],,,-)

AC_CHECK_HEADER([dmedia/audio.h],
		[AC_DEFINE(HAVE_DMEDIA_AUDIO_H,1,[have dmedia/audio.h?])
		 LIBS="$LIBS -laudio -laudiofile"],,-)

AC_CHECK_HEADER([Alib.h],
		[AC_DEFINE(HAVE_ALIB_H,1,[have Alib.h?]) LIBS="$LIBS -lAlib"],,-)

AC_CHECK_HEADER([esd.h],
		[AC_DEFINE(HAVE_ESD_H,1,[have esd.h?])
		 LIBS="$LIBS -lesd -laudiofile"],,-)

### Checks for libraries.

AC_CHECK_LIB_CXX(m, [#include <cmath>], [sin(0.0)],
		 [LIBS="-lm $LIBS"], [AC_MSG_ERROR(libm missing)])

AC_CHECK_LIB_CXX(png, [#include <png.h>], [png_destroy_read_struct(0,0,0)],
		 [LIBS="-lpng $LIBS"], [AC_MSG_ERROR(libpng missing)])

AC_CHECK_LIB_CXX(jpeg, [
#include <cstdio>
#include <jpeglib.h>],
[jpeg_create_decompress(0)],
[LIBS="-ljpeg $LIBS"], [AC_MSG_ERROR(libjpeg missing)])

AC_CHECK_LIB_CXX(z, [#include <zlib.h>], [gzopen(0,0)],
		 [LIBS="-lz $LIBS"], [AC_MSG_ERROR(libz missing)])

AC_CHECK_LIB_CXX(X11, [#include <X11/Xlib.h>], [XQueryColor(0,0,0)],
		 [LIBS="-lX11 $LIBS"], [AC_MSG_ERROR(missing libX11)])

AC_CHECK_LIB_CXX(GL, [#include <GL/gl.h>], [glEnd()],
		 [LIBS="-lGL $LIBS"], [AC_MSG_ERROR(missing libGL)])

AC_CHECK_LIB_CXX(GLU, [#include <GL/glu.h>], [gluErrorString(0)],
		 [LIBS="-lGLU $LIBS"], [AC_MSG_ERROR(missing libGLU)])

# add -lefence to LIBS for Electric Fence mem debugging

###

AC_MSG_CHECKING([whether OpenGL implementation is Mesa])

# display lists don't work at present with i686/linux/mesa

AC_EGREP_HEADER(MESA, [GL/gl.h],
		[AC_DEFINE(BROKEN_GL_DISPLAY_LISTS,1,[broken OpenGL display lists?])
                 AC_MSG_RESULT(yes)],
		[AC_MSG_RESULT(no)])

dnl Checks for typedefs, structures, and compiler characteristics.
dnl AC_TYPE_MODE_T
dnl AC_TYPE_SIZE_T
dnl AC_HEADER_TIME
dnl AC_STRUCT_TM

dnl Checks for library functions.
dnl AC_HEADER_STDC
dnl AC_FUNC_STRFTIME
dnl AC_CHECK_FUNCS(getcwd gettimeofday select strerror)

dnl Setup linking options

if test $need_rpath = yes; then
   LDFLAGS="$LDFLAGS -Wl,-rpath,\${exec_prefix}/lib"
fi
LDFLAGS="$LDFLAGS -L\${exec_prefix}/lib"

###

AC_MSG_CHECKING([for location of tcl+tk >= 8.4])

AC_ARG_WITH(tcltk,
	    [AC_HELP_STRING([--with-tcltk=DIR],
			    [specify where tcl+tk >= 8.4 are installed [/usr/local]])],
	    ,
	    with_tcltk=/usr/local)

AC_MSG_RESULT($with_tcltk)

# Need to *prepend* with_tcltk/include to CPPFLAGS, so that we find
# tcl.h, tk.h, etc. in with_tcltk rather than in the standard system
# location

CPPFLAGS="-I${with_tcltk}/include $CPPFLAGS"

if test $need_rpath = yes; then
   LDFLAGS="$LDFLAGS -Wl,-rpath,${with_tcltk}/lib"
fi
LDFLAGS="$LDFLAGS -L${with_tcltk}/lib"

AC_MSG_CHECKING([for tcl+tk versions])

if ! test -r ${with_tcltk}/lib/tclConfig.sh; then
   AC_MSG_ERROR([could not find tclConfig.sh in ${with_tcltk}/lib])
fi

if ! test -r ${with_tcltk}/lib/tkConfig.sh; then
   AC_MSG_ERROR([could not find tkConfig.sh in ${with_tcltk}/lib])
fi

TCL_VERSION=`sh -c ". ${with_tcltk}/lib/tclConfig.sh ; echo \\$TCL_VERSION"`
TK_VERSION=`sh -c ". ${with_tcltk}/lib/tkConfig.sh ; echo \\$TK_VERSION"`

AC_MSG_RESULT([tcl ($TCL_VERSION), tk ($TK_VERSION)])

LIBS="$LIBS -ltcl${TCL_VERSION} -ltk${TK_VERSION}"

###

AC_MSG_CHECKING([whether to enable matlab support])

AC_ARG_WITH(matlab,
	    [AC_HELP_STRING([--with-matlab],
			    [specify MATLAB root directory [/usr/local/matlab]])],
	    ,
	    with_matlab=/usr/local/matlab)

if test -d $with_matlab; then
   AC_MSG_RESULT($with_matlab)

   AC_MSG_CHECKING([for matlab architecture])
   case $OS in
      Linux)
	 matlab_arch=glnx86
         ;;
      *)
         AC_MSG_ERROR([matlab architecture unknown for '$OS'])
	 ;;
   esac
   AC_MSG_RESULT($matlab_arch)

   CPPFLAGS="$CPPFLAGS -I${with_matlab}/extern/include"
   matlab_lib=${with_matlab}/extern/lib/${matlab_arch}
   LDFLAGS="$LDFLAGS -L${matlab_lib}"
   if test $need_rpath = yes; then
      LDFLAGS="$LDFLAGS -Wl,-rpath,${matlab_lib}"
   fi
   AC_DEFINE(WITH_MATLAB,1,[with matlab support?])
else
   AC_MSG_RESULT(no)
   matlab_lib=""
fi

AC_SUBST(matlab_lib)

###

enable_readline=yes

AC_CHECK_HEADERS([readline/readline.h readline/history.h],
		  , enable_readline=no,-)

if test $enable_readline = yes; then

   AC_CHECK_LIB_CXX(termcap, [#include <termcap.h>], [tgetent(0,0)],
		    [LIBS="-ltermcap $LIBS"])

   AC_CHECK_LIB_CXX(readline,
		    [#include <stdio.h>
		     #include <readline/readline.h>],
		    [rl_callback_handler_install(0,0)],
		    [LIBS="-lreadline $LIBS";
                     AC_DEFINE(WITH_READLINE,1,[with readline support?])])
fi

###

AC_MSG_CHECKING([whether to enable debug mode])

AC_ARG_ENABLE(debug,
	      [AC_HELP_STRING([--enable-debug],
			      [whether the build should include debugging [yes]])],
	      ,
	      enable_debug=yes)

AC_MSG_RESULT($enable_debug)

if test $enable_debug = yes; then
   AC_DEFINE(PROF,1,[enable profiling?])
   CXXFLAGS="$CXXFLAGS $cxxflags_debug"
   AC_SUBST(LIB_SUFFIX, .d.${PACKAGE_VERSION})
   AC_SUBST(EXECUTABLE, \${exec_prefix}/bin/testsh)
else
   AC_DEFINE(NO_DEBUG,1,[statically turn off all debug statements?])
   CXXFLAGS="$CXXFLAGS $cxxflags_prod"
   AC_SUBST(LIB_SUFFIX, ${PACKAGE_VERSION})
   AC_SUBST(EXECUTABLE, \${exec_prefix}/bin/grsh${PACKAGE_VERSION})
fi

AC_CONFIG_FILES([Makefile:Makefile.in])

AC_OUTPUT()
